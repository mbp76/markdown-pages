defaultTasks "wrapper", "watch", "dependencies", "clean", "build", "test"

def aptPackages = ["libspock-java", "pandoc", "node-request", "node-less"]
def path = System.getProperty("user.dir")
def srcDir = "${path}/src"
def testDir = "${path}/test"
def buildDir = "${path}/docs"
def layout = "${srcDir}/template/layout.html"
def metadata = "${srcDir}/template/metadata.yaml"

task wrapper(type: Wrapper) {
  description "Creates the project wrapper."
  gradleVersion = "3.4.1"
}

task watch(type: Exec) {
  description "Watches for changes."
  inputs.files(srcDir, testDir)
  commandLine "true"

  new ByteArrayOutputStream()
}

task dependencies() {
  description "Installs all dependencies."

  dependsOn {
    tasks.findAll { task ->
      task.name.startsWith("dependencies") && !task.name.equals("dependencies")
    }
  }
}

task aptUpdate(type: Exec) {
  description "Updates APT repository information."
  workingDir path
  commandLine "apt-get"
  args "update"

  new ByteArrayOutputStream()
}

task dependenciesApt(type: Exec, dependsOn: aptUpdate) {
  description "Installs APT dependencies."
  workingDir path
  commandLine "apt-get"
  args = ["install", "-y"] + aptPackages

  new ByteArrayOutputStream()
}

task clean(type: Delete) {
  description "Cleans build and temporary files."
  delete "${buildDir}"

  dependsOn {
    tasks.findAll { task ->
      task.name.startsWith("clean") && !task.name.equals("clean")
    }
  }

  doLast {
    new File(buildDir).mkdir()
  }
}

task build() {
  description "Builds all project sources."

  dependsOn {
    tasks.findAll { task ->
      task.name.startsWith("build") && !task.name.equals("build")
    }
  }
}

fileTree(dir: srcDir, include: "**/*.md").each { File mdFile ->
  def fileName = mdFile.getName().split("\\.")[0]
  def htmlFile = "${buildDir}/${fileName}.html"
  def fileCamelCase = fileName.split("-").collect{ it.capitalize() }.join()

  task "build${fileCamelCase}"(type: Exec) {
    description "Builds ${mdFile.getName()} into an HTML page."
    workingDir path
    commandLine "pandoc"
    args mdFile, metadata, "-s", "-o", htmlFile, "-t", "html5", "--template",
      layout, "--no-highlight"

    new ByteArrayOutputStream()
  }
}

task buildAssets(type: Copy) {
  description "Builds the assets folder."
  from "${srcDir}/assets"
  into "${buildDir}/assets"
}

task buildStyles(type: Exec) {
  description "Builds main CSS preprocessor file into plain CSS."
  workingDir path
  commandLine "lessc"
  args "${srcDir}/style/main.less", "${buildDir}/style.css"

  new ByteArrayOutputStream()
}

task buildHtml(type: Copy) {
  description "Moves HTML files without changing them."
  from srcDir
  include "*.html"
  into buildDir
}

task test() {
  description "Runs all test cases."

  dependsOn {
    tasks.findAll { task ->
      task.name.startsWith("test") && !task.name.equals("test")
    }
  }
}

fileTree(dir: "${testDir}", include: "**/*.groovy").each { File testFile ->
  def fileName = testFile.getName().split("\\.")[0]

  task "test${fileName}"(type: Exec) {
    description "Runs the ${fileName} case."
    workingDir path
    commandLine "groovy"
    args "-cp", "/usr/share/java/spock-core.jar", testFile

    new ByteArrayOutputStream()
  }
}

task serve(type: Exec) {
  description "Serves the project on a development server."
  workingDir buildDir
  commandLine "python"
  args "-m", "SimpleHTTPServer", "8000"

  new ByteArrayOutputStream()
}
